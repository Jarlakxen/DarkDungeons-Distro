/* $Id: repsys.src 1256 2008-09-01 22:28:27Z AustinHeilman $
 *
 */

use uo;
use os;
use cfgfile;

include ":repsys:repsys";

// Stored as globals because hook scripts handle them in a special way.
var g_name_colors, g_general_elem;

program Install()
	Set_Critical(1);

	var rep_cfg := ReadConfigFile("::repsys");
	if ( !rep_cfg )
		return 0;
	endif

	g_name_colors := rep_cfg["NameColoring"];
	if ( !g_name_colors )
		return 0;
	endif

	g_general_elem := rep_cfg["General"];
	if ( !g_general_elem )
		return 0;
	endif

	Set_Critical(0);

	return 1;
endprogram

exported function NameColor(mobile, seen_by)
	var color := 0;

	if ( mobile.master )
		return NameColor(mobile.master, seen_by);
	elseif ( mobile.murderer || mobile.alignment == ALIGNMENT_EVIL )
		color := GetConfigInt(g_name_colors, "Murderer");
	elseif ( mobile.criminal || mobile.alignment == ALIGNMENT_NEUTRAL )
		// Murderers are always criminals
		// Criminals are not always murderers
		color := GetConfigInt(g_name_colors, "Criminal");
	elseif ( Guild_Ally(mobile, seen_by) )
		color :=  GetConfigInt(g_name_colors, "GuildAlly");
	elseif ( Guild_Enemy(mobile, seen_by) )
		color := GetConfigInt(g_name_colors, "GuildEnemy");
	elseif ( mobile.Enabled("invul") )
		color := GetConfigInt(g_name_colors, "Invulnerable");
	else
		color := GetConfigInt(g_name_colors, "Innocent");
	endif

	if ( color.errortext )
		SysLog("NameColor() - "+color.errortext);
	endif

	return CInt(color);
endfunction

exported function HighLightColor(mobile, seen_by)
	var color := 0;

	if ( mobile.master )
		return HighLightColor(mobile.master, seen_by);
	elseif ( mobile.murderer || mobile.alignment == ALIGNMENT_EVIL )
		color := GetConfigInt(g_name_colors, "MurdererHighLight");
	elseif ( mobile.criminal || mobile.alignment == ALIGNMENT_NEUTRAL )
		// Murderers are always criminals
		// Criminals are not always murderers
		color := GetConfigInt(g_name_colors, "CriminalHighLight");
	elseif ( Guild_Ally(mobile, seen_by) )
		color :=  GetConfigInt(g_name_colors, "GuildAllyHighLight");
	elseif ( Guild_Enemy(mobile, seen_by) )
		color := GetConfigInt(g_name_colors, "GuildEnemyHighLight");
	elseif ( mobile.Enabled("invul") )
		color := GetConfigInt(g_name_colors, "InvulnerableHighLight");
	else
		color := GetConfigInt(g_name_colors, "InnocentHighLight");
	endif

	if ( color.errortext )
		SysLog("HighLightColor() - "+color.errortext);
	endif

	return CInt(color);
endfunction

exported function OnAttack(attacker, defender)
	if ( attacker == defender )
		// Safe to attack.. yourself?
		return 1;
	elseif ( defender.master )
		// Check for the npc's master.
		return OnAttack(attacker, defender.master);
	elseif ( IsInnocentTo(defender, attacker) )
		// Reset the criminal timer for the attacker.
		attacker.SetCriminal(1);
	endif

	if ( IsAggressorTo(attacker, defender) )
		//Reset the aggressive timer if the defender was the aggressor.
		AddAggressorTo(attacker, defender);
	else
		//Attacker is the aggressor
		if ( !IsAggressorTo(defender, attacker) )
			// New engagement, inform the defender of the attack.
			PrintTextAbovePrivate(defender, "*"+attacker.name+" is attacking you!*",
			defender, _DEFAULT_TEXT_FONT, GetConfigInt(g_name_colors, "Murderer"));
		endif
		AddAggressorTo(defender, attacker);
	endif

	return 1;
endfunction

exported function OnDamage(attacker, defender)
	if ( attacker == defender )
		// It is okay to hurt yourself. Damn emos!
		return 1;
	endif

	// Initiate the damage as an attack.
	OnAttack(attacker, defender);

	if ( IsInnocentTo(defender, attacker) )
		// Attacker (may have) caused damage to defender.
		// Attacker can now be reported if defender dies.
		AddToBeReportable(defender, attacker);
	else
		// Restart the lawful damage timer for the attacker.
		// Attacker is lawfully damaging the defender.
		AddLawfullyDamagedTo(defender, attacker);
	endif

	return 1;
endfunction

exported function OnHelp(mobile, helped)
	if ( mobile == helped )
		// Its okay to help yourself.
		return 1;
	elseif ( helped.criminal )
		// Not okay to help criminals!
		mobile.SetCriminal(1);
	elseif ( helped.alignment == ALIGNMENT_EVIL )
		// Not okay to help red npcs!
		mobile.SetCriminal(1);
	endif

	return 1;
endfunction

function Guild_Ally(byref mobile, byref seen_by)
	if ( !mobile.guild )
		return 0;
	endif
	if ( !seen_by.guild )
		return 0;
	endif

	if ( mobile.guildid == seen_by.guildid )
		return 1;
	endif

	var mobile_guild := mobile.guild;
	var seen_by_guild := seen_by.guild;

	if ( mobile_guild.isallyguild(seen_by_guild) )
		return 1;
	endif
	if ( seen_by_guild.isallyguild(mobile_guild) )
		return 1;
	endif

	return 0;
endfunction

function Guild_Enemy(byref mobile, byref seen_by)
	if ( !mobile.guild )
		return 0;
	endif
	if ( !seen_by.guild )
		return 0;
	endif

	var mobile_guild := mobile.guild;
	var seen_by_guild := seen_by.guild;
	if ( mobile_guild.isenemyguild(seen_by_guild) )
		return 1;
	endif
	if ( seen_by_guild.isenemyguild(mobile_guild) )
		return 1;
	endif

	return 0;
endfunction