use uo;
use os;

include ":brainAI:npcNerves";
include ":brainAI:npcCommands";
include ":brainAI:npcWatchList";

program BrainNerve(params)
	Set_Critical(1);
	Set_Event_Queue_Size(200);
	var npc		:= params[1];
	//var nerve_name	:= params[2];
	var event	:= params[3];
	var settings	:= params[4];
	var scripts	:= params[5];
	params := 0; // Not needed anymore.
	Set_Critical(0);
	
	while ( npc )
		if ( event )
			case ( event.type )
				SYSEVENT_ENTEREDAREA:
					EnteredArea(npc, event, settings, scripts);
					break;
				default:
					break;
			endcase

			event := 0;
		endif

		event := Wait_For_Event(900000);
	endwhile
endprogram

function EnteredArea(npc, byref event, byref settings, byref scripts)
	var source := event.source;

	if ( NPC_ShouldWatch(npc, source, settings, scripts) )
		if ( NPC_AddToWatchList(npc, source) )
			AI_WakeUp(npc);

			return 1;
		endif
	endif

	return 0;
endfunction
