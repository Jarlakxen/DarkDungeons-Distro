// $Id$

use uo;
use os;
use util;

include ":brainAI:npcNerves";
include ":brainAI:npcCommands";
include ":brainAI:npcWatchList";
include ":attributes:attributes";

program BrainNerve(params)
	var npc		:= params[1];
	//var nerve_name	:= params[2];
	var settings	:= params[3];
	var scripts	:= params[4];
	params := 0; // Not needed anymore.
	
	while ( npc )
		if ( !npc.opponent )
			if ( NPC_LoadWatchList(npc).Size() < 1 )
				// No need to look around any longer. We're all alone :(
				return 0;
			else
				FindOpponent(npc, settings, scripts);
			endif
		endif
		
		AI_ClearThoughts(npc, CLR_NERVE);
		Sleep(5);
	endwhile
endprogram

function FindOpponent(npc, byref settings, byref scripts)
	var attack_list := array;
	foreach mobile in ListMobilesInLineOfSight(npc, CInt(settings["AreaSize"]))
		if ( NPC_ShouldWatch(npc, mobile, settings, scripts) )
			attack_list.Append(mobile);
		endif
		SleepMS(2);
	endforeach
	
	if ( attack_list.Size() > 0 )
		var victim := attack_list[1];
		
		// If there is more than one, attack the "strongest" one
		if ( attack_list.Size() > 1 )
			var victim_priority := GetPriority(victim);
			var temp_priority;
			for i := 2 to (attack_list.Size())
				var temp_priority := GetPriority(attack_list[i]);
				if ( temp_priority >= victim_priority )
					victim := attack_list[i];
					victim_priority := GetPriority(victim);
				endif
			endfor
		endif
		
		AI_Attack(npc, victim);
		return victim;
	endif
	
	return 0;
endfunction

// Blade spirits attack the ones with the most strength and tactics first
function GetPriority(mobile)
	return (AP_GetStat(mobile, STRENGTH) + AP_GetSkill(mobile, TACTICS));
endfunction