/*===============================================================
* Current Version
* MSG.SRC - v0.1
* Updated 9/17/2005 3:20AM
*
* -- Revision v0.1 --
* Austin
*  Created command
*
===============================================================*/

use uo;
use os;
use util;

include ":gumps:gumps";
include ":gumps:gumps_ex";
include ":datafile:datafile";

program command_Message(who, text)
	if ( CommandIssued(who, text) )
		return 1;
	else
		MessageGump(who);
	endif
endprogram

function MessageGump(who)
	var gump := GFCreateGump();

	GFResizePic(gump, 0, 0, GFGetCfgConst("Defaults", "BackGround"), 310, 420);
	GFResizePic(gump, 15, 15, GFGetCfgConst("Defaults", "ForeGround"), 280, 390);

	GFTextLine(gump, 25, 20, 2100, "Player Name");
	GFTextLine(gump, 255, 20, 2100, "Send");

	var names_list := GetNamesList(who);
	var num_players := names_list.Size();
	var counter := 0;
	var y_pos := 45;

	GFPage(gump, 1);
	foreach name in (names_list.Keys())
		GFTextLine(gump, 25, y_pos, 2100, name);
		GFAddButton(gump, 261, y_pos+3, 2117, 2118, GF_CLOSE_BTN, _name_iter);

		y_pos += 20;
		counter += 1;

		if ( counter >= 18 && _name_iter < num_players )
			GFAddButton(gump, 283, 385, 0xA92, 0xA93, GF_PAGE_BTN, gump.cur_page+1);
			GFPage(gump, gump.cur_page+1);
			GFAddButton(gump, 283, 40, 0xA90, 0xA91, GF_PAGE_BTN, gump.cur_page-1);
			counter := 0;
			y_pos := 40;
		endif
		SleepMS(2);
	endforeach

	var input := GFSendGump(who, gump);
	input := input[0];

	if ( !input )
		SendSysMessage(who, "Cancelled");
		return 0;
	else
		names_list := names_list.Keys();
		SendMessage(who, names_list[input]);
	endif
endfunction

function GetNamesList(who)
	var the_list := dictionary; // Used to store names in ABC order
	foreach person in EnumerateOnlineCharacters();
		var msg_settings := GetMessageSettings(person);

		if ( (msg_settings.disable) && (!who.cmdlevel) )
			//If messages are off, and 'who' is a player, dont add the name.
			//If 'who' has a command level, then he/she can see any name.
			continue;
		elseif ( (!msg_settings.visible) && (person.concealed > who.cmdlevel) );
			//If the person is not in visible mode, and concealed is
			//higher than who's command level, don't add that person.
			continue;
		else
			the_list.Insert(person.name, person.serial);
		endif

		SleepMS(2);
	endforeach

	return the_list;
endfunction

function SendMessage(who, name_text, is_reply:=0)
	var can_msg := 0;
	if ( is_reply )
		can_msg := 1;
	elseif ( name_text in (GetNamesList(who).Keys()) )
		can_msg := 1;
	endif

	if ( !can_msg )
		SendSysMessage(who, name_text+" is not online.");
		return 0;
	endif

	var message_txt := RequestInput(who, who.backpack, "What message would you like to send to "+name_text+"? (hit escape to cancel)");
	if ( !message_txt )
		SendSysMessage(who, "Cancelled.");
		return 0;
	endif

	var sndr_settings := GetMessageSettings(who);
	sndr_settings["last"] := name_text;
	SetObjProperty(who, ".msg", sndr_settings);

	foreach player in EnumerateOnlineCharacters()
		if ( player.name == name_text )
			SendSysMessage(player, "Message from "+who.name+" : "+message_txt);
			SendSysMessage(who, "You sent a message to ["+who.name+"]: "+message_txt);

			var rcvr_settings := GetMessageSettings(player);
			rcvr_settings["reply"] := who.name;
			SetObjProperty(player, ".msg", rcvr_settings);

			if( sndr_settings.Exists("disabled") )
				SendSysMessage(who, "Reminder: You have messages off.");
				SendSysMessage(player, "Note: "+who.name+" has messages off. Use .msg reply.");
			endif
		elseif ( player.cmdlevel < who.cmdlevel )
			var rcvr_settings := GetMessageSettings(player);
			if ( rcvr_settings.Exists("spy") )
				SendSysMessage(player, "[MSG SpyMode] From:"+who.name+" To:"+name_text+": "+message_txt);
			endif
		endif

		SleepMS(2);
	endforeach
endfunction

function CommandIssued(who, text)
	var msg_settings := GetMessageSettings(who);

	if ( text == "off" )
		msg_settings["disabled"] := 1;
		SetObjProperty(who, ".msg", msg_settings);
		SendSysMessage(who, "Your name has been removed from the .msg list.");
		return 1;
	elseif ( text == "on" )
		msg_settings.Erase("disabled");
		SetObjProperty(who, ".msg", msg_settings);
		SendSysMessage(who, "Your name has been put back on from the .msg list.");
		return 1;
	elseif ( text == "reply" )
		SendMessage(who, msg_settings["reply"], 1);
		return 1;
	elseif ( text == "last" )
		SendMessage(who, msg_settings["last"], 1);
		return 1;
	elseif ( text )
		SendMessage(who, text);
		return 1;
	endif

	if ( who.cmdlevel <= 1 )
		return 0;
	elseif ( text == "spy on" )
		msg_settings["spy"] := 1;
		SendSysMessage(who, "Private message viewing is enabled.");
		SetObjProperty(who, ".msg", msg_settings);
		return 1;
	elseif ( text == "spy off" )
		msg_settings.Erase("spy");
		SendSysMessage(who, "Private message viewing is disabled.");
		SetObjProperty(who, ".msg", msg_settings);
		return 1;
	elseif ( text == "visible on" )
		msg_settings["visible"] := 1;
		msg_settings.Erase("visible");
		SendSysMessage(who, "Players can now message you while concealed.");
		SetObjProperty(who, ".msg", msg_settings);
		return 1;
	elseif ( text == "visible off" )
		msg_settings.Erase("visible");
		SendSysMessage(who, "Players can no longer message you while concealed.");
		SetObjProperty(who, ".msg", msg_settings);
		return 1;
	else
		return 0;
	endif
endfunction

function GetMessageSettings(mobile)
	var msg_settings := GetObjProperty(mobile, ".msg");
	if ( Lower(TypeOf(msg_settings)) != "dictionary"  )
		msg_settings := dictionary;
	endif

	return msg_settings;
endfunction