/* $Id: writequip.src 486 2006-06-29 11:17:18Z austinheilman $
 *
 */

use cfgfile;
use uo;

program textcmd_writequip(who, tag)
	if ( !tag )
		SendSysMessage(who, "Usage: .writequip [equiptag]");
		return 0;
	endif

	var cfg_file := ReadConfigFile("::equip");
	if ( cfg_file[tag] )
		SendSysMessage(who, "Template already exists.");
		return 0;
	endif

	SendSysMessage(who, "Target the person to write the equipment of.");
	SendSysMessage(who, "Equipment will be tagged '"+tag+"'");
	var whom := Target(who);

	if ( whom )
		var props := array;
		var prop := struct{"name", "value"};

		var propcount := 0;

		foreach eqitem in ( ListEquippedItems(whom) )
			prop.name := "Equip";
			prop.value := Hex(eqitem.objtype);
			if ( eqitem.color )
				prop.value := prop.value+" "+Hex( eqitem.color);
			endif
			prop.value := PadStr(prop.value, 16);
			prop.value := prop.value+" //"+eqitem.desc;

			propcount += 1;
			props[ propcount ] := prop;
		endforeach

		AppendConfigFileElem("equip", "Equipment", tag, props);
	endif

	return 1;
endprogram

function PadStr( str, newlen )
	print( "PadStr in: '"+str+"'");
	var addcount := newlen - Len(str);
	while (addcount > 0)
		str := str+" ";
		addcount := addcount - 1;
	endwhile
	print( "PadStr out: '"+str+"'");
	return str;
endfunction