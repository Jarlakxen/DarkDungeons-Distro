/* $Id: realmwipe.src 381 2006-06-18 19:33:20Z austinheilman $
 *
 */

use uo;
use os;
use polsys;
use vitals;

include ":gumps:yesno";
include ":gumps:requestgump";

program command_WipeRealm(who)
	var realm_name := RequestGump(who, "Enter the realm name to wipe.", "", who.realm);
	
	var realm_info := Realms()[realm_name];
	if ( !realm_info )
		SendSysMessage(who, "Invalid realm name.");
		return 0;
	endif
	
	if ( !YesNo(who, "**WARNING** [P] This will remove all items and npcs from the realm.", "Yes, do it!", "Cancel") )
		SendSysMessage(who, "Cancelled.");
		return 0;
	endif
	
	foreach object in ( ListObjectsInBox(0, 0, -130, realm_info.width, realm_info.height, 130, realm_name) )
		if ( object.IsA(POLCLASS_ITEM) )
			DestroyItem(object);
		elseif ( object.IsA(POLCLASS_NPC) )
			object.Disable("invul");
			ApplyRawDamage(object, 12000);
		endif
		SleepMS(2);
	endforeach
	
	SendSysMessage(who, "Done.");
	return 1;
endprogram
