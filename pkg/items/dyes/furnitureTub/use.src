use uo;
use cfgfile;

include ":dyes:dyes";
include ":itemutils:itemdesc";

program UseTub(mobile, dyetub)
	if( !ReserveItem(dyetub) )
		SendSysMessage(mobile, "That dye tub is aleady in use.");
		return 0;
	endif

	SendSysMessage(mobile, "What do you want to use this dyetub on?");
	var targ := target(mobile);

	if ( !targ )
		SendSysMessage(mobile, "Cancelled");
		return 0;
	elseif( !ReserveItem(targ) )
		SendSysMessage(mobile, "That is already in use.");
		return 0;
	elseif( GetObjProperty((mobile.multi), "ownerserial") != mobile.serial )
		SendSysMessage(mobile, "You are not the owner of this house.");
		return 0;
	elseif( targ.movable )
		SendSysMessage(mobile, "That targ is not locked down.");
		return 0;
	endif

	if ( IsFurniture(targ) || mobile.cmdlevel >= 2 )
		PlaySoundEffect(mobile, 0x23F);
		targ.color := dyetub.color;
	else
		SendSysMessage(mobile, "You can not dye that.");
	endif
endprogram

function IsFurniture(targ)
	if ( targ.IsFurniture() )
		return 1;
	elseif ( GetItemDescInfo(targ.objtype).Furniture )
		return 1;
	else
		return 0;
	endif
endfunction
