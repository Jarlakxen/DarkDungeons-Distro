use uo;
use cfgfile;

include ":attributes:attributes";

program skill_Tracking( mobile )

	var config := ReadConfigFile( ":tracking:tracking" );
	if( config.errortext )
		SysLog("Error:: Unable to read config [:tracking:tracking] -->"+config.errortext);
		return 0;
	endif

	if( GetObjProperty( mobile, "#TrackingTimeout" ))
		SendSysMessage( mobile, "You are already tracking a creature." );
		return 0;
	endif

	if( !SkillCheck( mobile, TRACKING, -1 ))
		SendSysMessage( mobile, "You fail to find any creatures nearby." );
		return 0;
	endif

	var range := AP_GetSkill( mobile, TRACKING );

	var nearby := ListMobilesNearLocation( mobile.x, mobile.y, mobile.z, CInt( range ), mobile.realm );
	if( Len( nearby ) < 2 )
		SendSysMessage( mobile, "There don't seem to be any creatures nearby." );
		return 0;
	endif

	var track_list := CreateMenu( "Select a category." ),
	track_menu := CreateMenu( "Select a creature." ),
	animal := 0,
	monster := 0,
	person := 0,
	type;

	foreach character in nearby
		if( mobile.serial != character.serial && !character.dead )
			var elem := FindConfigElem( config, CInt( character.graphic ));
			if( elem )
				type := GetConfigString( elem, "Type" );
				case( type )
					"Animal"     : animal	:= 1;
					"Monster"    : monster	:= 1;
					"Human"      : person	:= 1;
				endcase
			endif
		endif
		SleepMS(1);
	endforeach

	if( animal )
		AddMenuItem( track_list, 0x20cf, "Animals" );
	endif
	if( monster )
		if( AP_GetSkill( mobile, TRACKING ) > 75 )
			AddMenuItem( track_list, 0x20d9, "Monster" );
		endif
	endif
	if( person )
		if( AP_GetSkill( mobile, TRACKING ) > 120 )
			AddMenuItem( track_list, 0x2106, "Human" );
		endif
	endif

	var selection := SelectMenuItem2( mobile, track_list ),
	listtype := {};

	case( selection.objtype )
		8399: listtype := "Animal"; break;
		8409: listtype := "Monster"; break;
		8454: listtype := "Human"; break;
	endcase

	var final_type,
	ids := array,
	count := 0;

	foreach character in nearby
		if( mobile.serial != character.serial && !character.dead )
			var elem := FindConfigElem( config, character.graphic );
			if( elem )
				final_type := GetConfigString( elem, "Type" );
				if( final_type == listtype )
					count += 1;
					ids[count] := character;
					type := GetConfigInt( elem, "Tile" );
					AddMenuItem( track_menu, type, character.name );
				endif
			endif
		endif
		SleepMS(1);
	endforeach

	if( count > 0 )
		selection := SelectMenuItem2( mobile, track_menu );
		if( !selection )
			SendSysMessage( mobile, "Cancelled." );
			return 0;
		endif

		var critter := ids[selection.index];
		SetObjProperty( mobile, "#TrackingTimeout", 1 );

		Detach();
		var timer := 0;
		while( Distance( mobile, critter ) <= range && !critter.dead && timer <= 12 )
			timer += 1;
			var dir := Get_direction( mobile, critter );
			if( dir == "nearby" )
				SendSysMessage( mobile, critter.name+" is nearby." );
                                EraseObjProperty( mobile, "#TrackingTimeout" );
                                return 1;
			else
				SendSysMessage( mobile, critter.name+" is to the "+dir+"." );
			endif
			Sleep(5);
		endwhile

		SendSysMessage( mobile, "You have lost your quarry." );
		EraseObjProperty( mobile, "#TrackingTimeout" );
	endif

	return 1;
endprogram

function Get_direction( mobile, critter )

	var ns,
	    ew,
	    desc;

	if( mobile.y > critter.y )
		ns := "north";
	elseif( mobile.y < critter.y )
		ns := "south";
	endif

	if( mobile.x > critter.x )
		ew := "west";
	elseif( mobile.x < critter.x )
		ew := "east";
	endif

	if( !ns && !ew )
		desc := "nearby";
	else
		desc := ns +"-"+ ew;
	endif

	return CStr( desc );
endfunction
