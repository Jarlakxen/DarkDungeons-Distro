// $Id: paralyzeField.src 1172 2008-02-05 20:09:05Z AustinHeilman $

use uo;
use os;

include ":magery:spells";
include ":magery:fields";
include ":attributes:attributes";
include "include/facings";

program SpellScript_ParalyzeField(params)
	var mobile := params[1];
	var info := params[2];
	var targ := params[3];
	params := 0;

	MS_PlaySpellSFX(info.spell_id, targ);
	MS_PlaySpellGFX(info.spell_id, targ, mobile);

	var objtype;
	var direction := GetDirectionByFacing(mobile);
	case ( direction )
		FIELD_SHAPE_HORIZONTAL: objtype := "ParaFieldEW"; break;
		FIELD_SHAPE_VERTICAL: objtype := "ParaFieldNS"; break;
	endcase

	// Duration: 33% Magery +3 seconds
	var duration := CInt((AP_GetSkill(mobile, MAGERY) * 0.33) + 3);
	// Paralyze Duration: ((caster Eval Int/10) - ((target Resist Magic/10)) * 3 seconds
	// Note: On OSI the duration max is 42.6, but we use damage dice, so max is 42. :(
	var para_duration := CInt((AP_GetSkill(mobile, EVALUATING_INTELLIGENCE) - (AP_GetSkill(targ, RESISTING_SPELLS) / 10)) * 3);
	var area_size := GetConfigInt(info.spell_config, "AreaSize");

	var coordinates := GetCoordinatesForField(targ.x, targ.y, direction, CInt(area_size/2));
	foreach coord in ( coordinates )
		var z := targ.z;
		if ( z < GetStandingHeight(coord.x, coord.y, targ.z, mobile.realm) )
			z := GetStandingHeight(coord.x, coord.y, targ.z, mobile.realm);
		endif

		if ( !CheckLosAt(mobile, coord.x, coord.y, z ) )
			continue; // Only create a field piece if the caster has line of sight.
		endif

		var item := CreateItemAtLocation(coord.x, coord.y, z, objtype, 1, mobile.realm);
		if ( item.errortext )
			SendSysMessage(mobile, "Error - "+item.errortext);
			continue;
		endif

		SetObjProperty(item, "SummonedField", mobile.serial);
		SetObjProperty(item, "ParaDuration", "1d"+para_duration);
		item.SetOwner(mobile);
		item.SetDuration(duration);

		SleepMS(2);
	endforeach

	return 1;
endprogram
