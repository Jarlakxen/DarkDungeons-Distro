// $Id$

use uo;
use os;

include ":magery:spells";
include ":magery:fields";
include ":attributes:attributes";
include "include/facings";

program SpellScript_PoisonField(params)
	var mobile := params[1];
	var info := params[2];
	var targ := params[3];
	params := 0;

	MS_PlaySpellSFX(info.spell_id, targ);
	MS_PlaySpellGFX(info.spell_id, targ, mobile);

	var objtype;
	var direction := GetDirectionByFacing(mobile);
	case ( direction )
		FIELD_SHAPE_HORIZONTAL: objtype := "PoisonFieldEW"; break;
		FIELD_SHAPE_VERTICAL: objtype := "PoisonFieldNS"; break;
	endcase

	// Duration: (3 + (Magery * 0.4)) seconds
	var duration := CInt(3 + AP_GetSkill(mobile, MAGERY) * 0.4);
	// Level is the same as Poison spell
	var poison_level := MS_GetPoisonLevel(mobile, targ);
	var area_size := GetConfigInt(info.spell_config, "AreaSize");

	var coordinates := GetCoordinatesForField(targ.x, targ.y, direction, CInt(area_size/2));
	foreach coord in ( coordinates )
		var z := targ.z;
		if ( z < GetStandingHeight(coord.x, coord.y, targ.z, mobile.realm) )
			z := GetStandingHeight(coord.x, coord.y, targ.z, mobile.realm);
		endif

		if ( !CheckLosAt(mobile, coord.x, coord.y, z ) )
			continue; // Only create a field piece if the caster has line of sight.
		endif

		var item := CreateItemAtLocation(coord.x, coord.y, z, objtype, 1, mobile.realm);
		if ( item.errortext )
			SendSysMessage(mobile, "Error - "+item.errortext);
			continue;
		endif

		SetObjProperty(item, "SummonedField", mobile.serial);
		SetObjProperty(item, "Poisonlevel", poison_level);
		item.SetOwner(mobile);
		item.SetDuration(duration);

		SleepMS(2);
	endforeach

	return 1;
endprogram
