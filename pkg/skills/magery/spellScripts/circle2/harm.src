// $Id: harm.src 1013 2007-03-05 22:46:11Z tekproxy $

use uo;
use os;

include ":magery:spells";
include ":attributes:attributes";
include ":damage:damage";

program SpellScript_Harm(params)
	var mobile := params[1];
	var info := params[2];
	var targ := params[3];
	params := 0;

	if ( !MS_MobileCheck(mobile, targ) )
		return 0;
	endif
	
	MS_PlaySpellSFX(info.spell_id, targ);
	MS_PlaySpellGFX(info.spell_id, targ, mobile);
	
	// Base damage is a decimal number in between min and max damage
	var min_damage := CInt(GetConfigInt(info.spell_config, "MinBaseDamage"));
	var max_damage := CInt(GetConfigInt(info.spell_config, "MaxBaseDamage"));
	var base_damage := min_damage + (CDbl(RandomInt(CInt((max_damage - min_damage) * 10)) + 1) / 10);
	var damage := MS_GetSpellDamage(mobile, targ, base_damage);

	// 1 tile = full damage, 2 tiles = half damage, >3 tiles = 1/4 damage
	case ( Distance(mobile, targ) )
		0:
		1: break;
		2: damage := CDbl(damage) / 2;
		default: damage := CDbl(damage) / 4;
	endcase

	ApplyDamageEX(targ, damage, info.damage_type, mobile);
	
	return 1;
endprogram
