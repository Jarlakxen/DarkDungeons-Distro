// $Id: magicTrap.src 1013 2007-03-05 22:46:11Z tekproxy $

use uo;
use os;

include ":magery:spells";
include ":traps:traps";

program SpellScript_MagicTrap(params)
	var mobile := params[1];
	var info := params[2];
	var targ := params[3];
	params := 0;
	
	if ( !targ.IsA(POLCLASS_CONTAINER) )
		SendSysMessage(mobile, "This spell wont work on that!"); // what is the cliloc?
		return 0;
	endif

	foreach trap_info in (GetTrapList(targ))
		if ( trap_info.Name == "magicTrap" )
			SendSysMessage(mobile, "This container is already trapped!");
			return 0;
		endif
	endforeach
	
	MS_PlaySpellSFX(info.spell_id, targ);
	MS_PlaySpellGFX(info.spell_id, targ, mobile);
	
	// uo.com says 10-50 damage
	AddTrap(targ, "magicTrap", "10d5");
	
	return 1;
endprogram
