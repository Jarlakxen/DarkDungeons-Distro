// $Id$

use uo;
use os;

include ":magery:spells";
include ":traps:traps";

program SpellScript_MagicUntrap(params)
	var mobile := params[1];
	var info := params[2];
	var targ := params[3];
	params := 0;
	
	if ( !targ.IsA(POLCLASS_CONTAINER) )
		SendSysMessage(mobile, "This spell wont work on that!"); // what is the cliloc?
		return 0;
	endif
	
	var trap_list := GetTrapList(targ);
	var index := 0;
	
	for i := 1 to trap_list.Size()
		if ( (trap_list[i]).Name == "magicTrap" )
			index := i;
			break;
		endif
	endfor
	
	if ( !index )
		SendSysMessage(mobile, "This container isn't trapped!");
		return 0;
	endif
	
	MS_PlaySpellSFX(info.spell_id, targ);
	MS_PlaySpellGFX(info.spell_id, targ, mobile);
	
	RemoveTrap(targ, index);
	
	return 1;
endprogram
