// $Id$

use uo;
use os;

include ":magery:spells";
include ":attributes:attributes";
include ":timedscripts:timedScripts";

program SpellScript_ManaDrain(params)
	var mobile := params[1];
	var info := params[2];
	var targ := params[3];
	params := 0;

	if ( !MS_MobileCheck(mobile, targ) )
		return 0;
	endif

	if ( mobile.serial == targ.serial )
		SendSysMessage(mobile, "You cannot mana drain yourself!");
		return 0;
	endif
	
	MS_PlaySpellSFX(info.spell_id, targ);
	MS_PlaySpellGFX(info.spell_id, targ, mobile);

	// Mana drained = 40 + evaluating intelligence - target's resisting spells
	var mana_drained := 40 + AP_GetSkill(mobile, EVALUATING_INTELLIGENCE) - AP_GetSkill(targ, RESISTING_SPELLS);
	var targ_mana := AP_GetVital(targ, MANA);

	// Can not drain more mana than the target has
	if ( mana_drained > targ_mana )
		mana_drained := targ_mana;
	endif
	
	// Drain mana from targ and restore lost points after 4 seconds
	TS_StartTimer(targ, "ManaDrain", 4, mana_drained, mobile);
	
	return 1;
endprogram
