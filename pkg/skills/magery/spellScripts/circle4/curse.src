// $Id: curse.src 1017 2007-03-08 04:36:27Z tekproxy $

use uo;
use os;
use math;

include ":magery:spells";
include ":attributes:attributes";
include ":timedscripts:timedScripts";

program SpellScript_Curse(params)
	var mobile := params[1];
	var info := params[2];
	var targ := params[3];
	params := 0;
	
	if ( !MS_MobileCheck(mobile, targ) )
		return 0;
	endif

	// Note: Skill gain is a little too easy
	var resist_skill := AP_GetSkill(mobile, RESISTING_SPELLS);
	Print("diff b:"+(15 * (resist_skill / 100.0)));
	var diff := info.difficulty + (15 * (resist_skill / 100.0));
	var adv_diff_bonus := info.difficulty + CInt(((RandomInt(150) + 1.0) - (15 * (resist_skill / 100.0) * 10)) / 10.0);
	if ( adv_diff_bonus < 0 )
			adv_diff_bonus := 0;
	endif
	var adv_diff := resist_skill + adv_diff_bonus;
	Print("diff:"+diff+" adv_diff:"+adv_diff);
/*
	for i := 1 to 1000
		diff := info.difficulty + (15 * (resist_skill / 100.0));
		adv_diff_bonus := info.difficulty + CInt(((RandomInt(150) + 1.0) - (15 * (resist_skill / 100.0) * 10)) / 10.0);
		if ( adv_diff_bonus < 0 )
				adv_diff_bonus := 0;
		endif
		adv_diff := resist_skill + adv_diff_bonus;
		SkillCheck(mobile, RESISTING_SPELLS, diff, adv_diff);
		SleepMS(2);
	endfor
*/
	MS_PlaySpellSFX(info.spell_id, targ);
	MS_PlaySpellGFX(info.spell_id, targ, mobile);
	
	// Duration: 120% Magery, up to 144 seconds
	var duration := CInt(AP_GetSkill(mobile, MAGERY) * 1.2);
	if ( duration > 144 )
			duration := 144;
	endif
	
	// Decrease % = (Caster Magery / 10) + 1) - (Target Resist / 15)
	// Note: OSI says resist scales down potency but doesn't explain HOW
	var percent := CDbl((AP_GetSkill(mobile, MAGERY) / 10) + 1) - (AP_GetSkill(targ, RESISTING_SPELLS) / 15);
	var dex_points := CInt(AP_GetStat(targ, DEXTERITY) * (percent / 100));
	var int_points := CInt(AP_GetStat(targ, INTELLIGENCE) * (percent / 100));
	var str_points := CInt(AP_GetStat(targ, STRENGTH) * (percent / 100));
	
	TS_StartTimer(targ, "Clumsy", duration, dex_points, mobile);
	TS_StartTimer(targ, "FeebleMind", duration, int_points, mobile);
	TS_StartTimer(targ, "Weaken", duration, str_points, mobile);

	return 1;
endprogram
