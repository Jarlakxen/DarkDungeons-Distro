// $Id$

use uo;
use os;

include ":magery:spells";
include ":attributes:attributes";
include ":timedscripts:timedScripts";

program SpellScript_Bless(params)
	var mobile := params[1];
	var info := params[2];
	var targ := params[3];
	params := 0;
	
	if ( !MS_MobileCheck(mobile, targ) )
		return 0;
	endif
	
	MS_PlaySpellSFX(info.spell_id, targ);
	MS_PlaySpellGFX(info.spell_id, targ, mobile);
	
		// Str increase % = (Eval / 10 + 1)
	var percent := CDbl(AP_GetSkill(mobile, EVALUATING_INTELLIGENCE) / 10) + 1;
	var dex_points := CInt(AP_GetStat(targ, DEXTERITY) * (percent / 100));
	var int_points := CInt(AP_GetStat(targ, INTELLIGENCE) * (percent / 100));
	var str_points := CInt(AP_GetStat(targ, STRENGTH) * (percent / 100));

	// Duration: ((Eval / 5) + 1) * 6
	var duration := ((AP_GetSkill(mobile, EVALUATING_INTELLIGENCE) / 5) + 1) * 6;
	
	// Same as casting agility, cunning and strength all at once.
	// Bless should probably not be able to stack with Agility, etc. So we do it this way.
	TS_StartTimer(targ, "Agility", duration, dex_points, mobile);
	TS_StartTimer(targ, "Cunning", duration, int_points, mobile);
	TS_StartTimer(targ, "Strength", duration, str_points, mobile);
	
	return 1;
endprogram
