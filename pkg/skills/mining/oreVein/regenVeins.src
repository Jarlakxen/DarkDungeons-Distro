/*
 * $Id: regenVeins.src 504 2006-06-29 15:29:21Z austinheilman $
 *
 */
use uo;
use os;

include ":datafile:datafile";
include ":mining:settings";
include ":mining:report";

program GlobalControl()
	MSP_ReportText("---=[ ORE VEIN REGENERATOR HAS STARTED UP ]=---", MSP_REPORT_SYSLOG);
	
	var settings := MSP_GetSettingsCfgElem("Settings");
	var cycle_wait := CInt(settings.RegenWait);
	if ( cycle_wait < 1 )
		cycle_wait := 30;
	endif
	cycle_wait *= 60;
	
	SetGlobalProperty("#OreVeinRegenPId", GetPid());
	
	Set_Priority(1);
	while( 1 )
		Sleep(cycle_wait);
		RegenVeins();
	endwhile
endprogram

function RegenVeins()
	MSP_ReportText("Running ore vein regeneration cycle...", MSP_REPORT_SYSLOG);
	var data_file := DFOpenDataFile("VeinInfo", DF_CREATE);
	
	foreach elem_name in ( DFGetElemNames(data_file) )
		var ore_vein := SystemFindObjectBySerial(elem_name);
		var amount := ore_vein.GetRemainingOre();
		amount += 1;
		
		ore_vein.SetRemainingOre(amount);
		
		Sleep(1);
	endforeach
	
	return 1;
endfunction
