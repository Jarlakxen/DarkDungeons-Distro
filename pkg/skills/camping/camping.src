// $Id: camping.src 954 2007-01-17 18:44:52Z sroyalty $

include ":attributes:attributeConstants";
include ":attributes:attributes";
include ":attributes:skillCheck";

CONST UOBJ_CAMPFIRE := 0xDE3;
CONST UOBJ_ASHES    := 0xDEA;

program UseWood( who, wood )

	EraseObjProperty(who, "#IsMeditating");
	EraseObjProperty(who, "#HealTimer");

	// Check to make sure 
	if ( !ReserveItem(wood) )
		SendSysMessage(who, "That is already in use.");
		return 0;
	elseif( (!wood.movable) || !ReserveItem(wood) )
		SendSysMessage(who, "You cannot light that.");
		return 0;
	elseif( wood.container )
		PrintTextAbovePrivate( wood, "Put that on the ground first!", who );
		return 0;
	endif

	if( wood.amount > 1 )
		PrintTextAbovePrivate( wood, "Try lighting a smaller pile first!", who );
		return 0;
	endif

	var camptimer := GetObjProperty(who, "#camped");
	if( camptimer && !camptimer.errortext )
		if( camptimer > ReadGameClock() )
			SendSysMessage(who, "You must wait a few moments to camp again.");
			return 0;
		endif
	endif

	if( SkillCheck(who, CAMPING, -1) > 0 )
		SetObjProperty(who, "#camped", ReadGameClock() + 10);
		var x := wood.x;
		var y := wood.y;
		var z := wood.z;
		var wx := who.x;
		var wy := who.y;
		var realm := wood.realm;
		Detach();
		var campfire := CreateItemAtLocation( x, y, z, UOBJ_CAMPFIRE, 1, realm );
		campfire.movable := 0;
		DestroyItem( wood );
		var secured := 10;
		SendSysMessage( who, "You must wait " + secured + " seconds to prepare camp.");
		while( secured > 0 )
			if( (who.x != wx) || (who.y != wy) )
				SendSysMessage( who, "You abandon your preparations to camp.");
				break;
			endif
			secured := secured - 5;
			sleep(5);
		endwhile
		if( secured == 0 )
			SendSysMessage( who, "Your campfire is now secure" );
			SetObjProperty( who, "#camped", ReadGameClock() + 30 );
		endif
		sleep( 50 );
		DestroyItem( campfire );
		campfire := CreateItemAtLocation( x, y, z, UOBJ_ASHES, 1, realm);
		campfire.movable := 0;
		sleep( 60 );
		DestroyItem( campfire );
	else
		PrintTextAbovePrivate( wood, "You couldn't manage to start the fire.", who );
	endif

endprogram
