use uo;
use os;
use util;
use cfgfile;

include ":alchemy:potionKegs";

program use_potionkeg(who, keg)

	if( !ReserveItem(keg) )
		SendSysMessage(keg, "You cannot use that");
		return 0;
	elseif( !keg.movable )
		// Ok, it's locked down in a house maybe? Check Owner
		// and see if the user is the owner marked at lockdown.
		// Housing must do this also for Furniture in deeds!
		if( GetObjProperty(keg, "OwnerSerial") != who.serial )
			SendSysMessage(who, "That item does not belong to you.");
			return 0;
		endif
	endif

	EraseObjProperty(who, "#IsMeditating");
	EraseObjProperty(who, "#HealTimer");

	var type := GetObjProperty(keg, "PotionType");
	var amt  := 0;
	foreach thing in EnumerateItemsInContainer(keg)
		if(thing.objtype == 0xf0e)
			amt := amt + thing.amount;
		endif
	endforeach
	if(GetObjProperty(keg, "Vendored"))
		SendSysMessage(who, "You cannot use that while it's on a vendor.");
		return 0;
	endif
	if(amt == 0)
		SendSysMessage(who, "That keg is empty.");
		return 0;
	else
		if(keg.movable == 0)
			var ownerserial := GetObjProperty(keg, "OwnerSerial");
			if(ownerserial)
				if(ownerserial != who.serial)
					var hs := GetObjProperty(keg, "houseserial");
					var friend := 0;
					if(hs)
						var house := SystemFindObjectBySerial(hs);
						foreach thing in GetObjProperty(house, "coowners")
							if(thing == who.serial)
								friend := 1;
								break;
							endif
						endforeach
						if(friend == 0)
							foreach thing in GetObjProperty(house, "friends")
								if(thing == who.serial)
									friend := 1;
									break;
								endif
							endforeach
						endif
						if(friend == 0)
							SendSysMessage(who, "That does not belong to you.");
							return 0;
						endif
					else
						SendSysMessage(who, "That does not belong to you.");
						return 0;
					endif
				endif
			else
				SendSysMessage(who, "That does not belong to you.");
				return 0;
			endif
		endif
	endif

	if(!amt)
		SendSysMessage(who, "The keg is currently empty.");
		return;
	endif
	var potion := 0;
	foreach thingie in EnumerateItemsInContainer(who.backpack)
		if(thingie.container.objtype != 0x1940)
			if(thingie.objtype == 0xf0e)
				potion := thingie;
				break;
			endif
		endif
	endforeach
	if(potion == 0)
		SendSysMessage(who, "You are out of bottles.");
		return 0;
	endif
	if(SubtractAmount(potion, 1))
		var check := 0;
		foreach thing in EnumerateItemsInContainer(keg)
			if(thing.objtype == 0xf0e)
				if(SubtractAmount(thing, 1))
					check := 1;
					break;
				endif
			endif
		endforeach
		if(check == 1)
			PlaySoundEffect(who, 0x241);
			var the_potion := CreateItemInBackpack(who, type, 1);
			SetName(the_potion, lower(GetObjProperty(keg, "PotionName")));
			if(amt)
				rename_keg(keg, type);
			else
				SetName(keg, "a potion keg");
			endif
			SendSysMessage(who, "You fill a bottle with liquid from the keg.");
		endif
	endif

endprogram
