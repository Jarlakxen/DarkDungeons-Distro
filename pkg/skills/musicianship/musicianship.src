// $Id$

use basic;
use uo;
use os;

include ":attributes:attributes";
include ":instruments:musicinstruments";
include ":crafting:old/include/toolWear";

program use_Instrument(who, item)

	if( !Accessible(who, item) )
		SendSysMessage(who, "You cannot access that.");
		return 0;
	elseif( CInt(GetObjProperty(who, "#LastMusic")) > ReadGameClock() )
    		SendSysMessage(who, "You must wait a moment before playing again.");
    		return 0;
	elseif( !(item in EnumerateItemsInContainer(who.backpack)) )
		SendSysMessage(who, "That item is not in your backpack.");
		return 0;
	elseif( !ReserveItem(item) )
		SendSysMessage(who, "You cannot use that");
		return 0;
	endif

	EraseObjProperty(who, "#IsMeditating");
	EraseObjProperty(who, "#HealTimer");
	
	SetObjProperty(who, "#LastMusic", CInt(ReadGameClock() + 2));

	var pts := AP_GetSkill(who, MUSICIANSHIP) + 10;
	if(pts < 10)
		pts := 10;
	endif

	if( SkillCheck(who, MUSICIANSHIP, -1, pts) > 0 )
		item.Play(who, SUCCESS_SOUND);
	else	
		item.Play(who, FAIL_SOUND);
	endif

	CheckToolWear (who, item, MUSICIANSHIP);

endprogram
