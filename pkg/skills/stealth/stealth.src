/* $Id$
 *
 * http://uo.stratics.com/content/skills/stealth.shtml
 *
 */

use uo;
use os;
use math;

include ":attributes:attributes";
include "include/client";

program skill_Stealth(mobile)
	if ( AP_GetSkill(mobile, HIDING) < 50 )
		PrintTextAbovePrivate(mobile, "You are not good enough at hiding to stealth.", mobile);
		return 0;
	elseif ( (!mobile.hidden) )
		PrintTextAbovePrivate(mobile, "You must be hidden to use stealth.", mobile);
		return 0;
	elseif ( GetEquipmentByLayer(mobile, LAYER_MOUNT ) )
		PrintTextAbovePrivate(mobile, "You can't stealth while riding a horse.", mobile);
		return 0;
	endif

	if ( SkillCheck(mobile, STEALTH, -1) > 0 )
		mobile.stealthsteps := CalcNumSteps(mobile);
		PrintTextAbovePrivate(mobile, "You are now moving stealthily.", mobile);

		return 1;
	else
		if ( mobile.hidden )
			mobile.hidden := 0;
			PrintTextAbovePrivate(mobile, "You have revealed yourself.", mobile);
		else
			PrintTextAbovePrivate(mobile, "You fail to move stealthily.", mobile);
		endif

		return 0;
	endif
endprogram

function CalcNumSteps(mobile)
	var skill := AP_GetSkill(mobile, STEALTH);
	var num_steps := skill / 5;

	return CInt(Ceil(num_steps));
endfunction

