/*
 * $Id$
 *
 */

use uo;
use os;
use cfgfile;

include ":gumps:gumps";
include ":gumps:gumps_ex";

program textcmd_MakeSet(who, set_name)
	var config := ReadConfigFile(":itemutils:sets");

	if ( !set_name )
		set_name := SetNamesGump(who, config);
		if ( !set_name )
			SendSysMessage(who, "Cancelled.");
			return 0;
		endif
	elseif ( !config[set_name] )
		SendSysMessage(who, "Could not find set name "+set_name);
		return 0;
	endif

	SendSysMessage(who, "Target where you want to place "+set_name);
	var targ := TargetCoordinates(who);
	
	//From AddSet.src
	//var iteminfo := struct{"name":="//Item", "value":="ObjType Graphic Color X Y Z"};
	foreach line in ( GetConfigStringArray(config[set_name], "Item") )
		line := SplitWords(line);
		var objtype := CInt(line[1]), graphic := CInt(line[2]), color := CInt(line[3]);
		var x := CInt(line[4]), y := CInt(line[5]), z := CInt(line[6]);

		var item := CreateItemAtLocation(targ.x+x, targ.y+y, targ.z+z, objtype, 1, who.realm);
		item.color := color;
		item.graphic := graphic;
		item.movable := 0;
		if ( !item )
			SendSysMessage(who, "Error creating line! "+item.errortext);
		else
			//SendSysMessage(who, "Created "+item.desc+" at "+item.x+" "+item.y+" "+item.z);
		endif

		SleepMS(2);
	endforeach

	SendSysMessage(who, "Done.");
	return 1;
endprogram

function SetNamesGump(byref who, byref config)
	var elem_names := GetConfigStringKeys(config);

	var gump := GFCreateGump();
	GFResizePic(gump, 0, 0, GFGetCfgConst("Defaults", "BackGround"), 250, 450);
	GFResizePic(gump, 15, 15, GFGetCfgConst("Defaults", "ForeGround"), 220, 420);
	GFPage(gump, 1);

	var y_pos := 25;
	foreach elem_name in ( elem_names )
		GFAddButton(gump, 20, y_pos+5, 2103, 2104, GF_CLOSE_BTN, _elem_name_iter);
		GFTextLine(gump, 40, y_pos, 2100, elem_name);

		y_pos += 20;
		
		if ( _elem_name_iter % 20 == 0 )
			y_pos := 25;
			GFAddButton(gump, 210, 410, 2648, 2649, GF_PAGE_BTN, gump.cur_page+1);
			GFPage(gump, gump.cur_page+1);
			GFAddButton(gump, 210, 20, 2650, 2651, GF_PAGE_BTN, gump.cur_page-1);
		endif

		SleepMS(2);
	endforeach

	var input := GFSendGump(who, gump);
	input := input[0];
	if ( !input )
		return 0;
	endif

	SendSysMessage(who, "->"+elem_names[input]);
	return elem_names[input];
endfunction
